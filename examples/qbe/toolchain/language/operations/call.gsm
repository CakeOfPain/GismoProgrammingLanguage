
symbol() ::= $RAISE($1, "Undefined Function")
symbol(*) ::= $RAISE($1, "Undefined Function")

// Function Calls 
FunctionProto() ::= {
    fn_meta ::= $UNTYPE($1)
    
    $IF($EQUALS($VECTOR_LEN(fn_meta.arguments), 0), {}, {
        $RAISE($$, $CAT("Argument count mismatch for function ", fn_meta.name))
    })

    retAddr ::= $NEWADDR()
    retTypeStr ::= $UNTYPE(fn_meta.returnType).nativeType

    $WRITE("\t")
    $IF($EQUALS(retTypeStr, ""), {}, { // ELSE:
        $WRITE(retAddr)
        $WRITE(" =")
        $WRITE(retTypeStr)
        $WRITE(" ")
    })
    $WRITE("call $")
    $WRITE(fn_meta.addr)
    $WRITE("()\n")

    template ::= $QUOTE($TYPEDEF($Value(fn_meta.returnType, retAddr), X))
    $EVAL($REPLACE(template, X, $UNTYPE(fn_meta.returnType).typeName))
}

FunctionProto(*) ::= {
    fn_meta ::= $UNTYPE($1)
    given_args ::= $FLATTEN($QUOTE($2), ",") map $LAMBDA(x, $EVAL(x))

    $IF($EQUALS($VECTOR_LEN(fn_meta.arguments), $VECTOR_LEN(given_args)), {}, {
        $RAISE($$, $CAT("Argument count mismatch for function ", fn_meta.name))
    })

    formatted_args ::= (fn_meta.arguments zip given_args) map $LAMBDA(arg_def, {
        fn_meta_arg ::= $CAR(arg_def)
        given_arg ::= $CDR(arg_def)

        expected_native_type ::= TYPE2NATIVE(fn_meta_arg.type)
        expected_type ::= TYPE2TYPENAME(fn_meta_arg.type)

        isNativeType ::= $EVAL($REPLACE($QUOTE($TYPEOF(given_arg, T)), T, expected_native_type))
        isCorrectType ::= $EVAL($REPLACE($QUOTE($TYPEOF(given_arg, T)), T, expected_type))

        $IF(isNativeType, {
            $IF($TYPEOF(given_arg, string), {
                index ::= $DECLARE_STRING(given_arg)
                $CAT("l $str", index)
            }, $IF($TYPEOF(given_arg, float), {
                $CAT($UNTYPE(fn_meta_arg.type).nativeType, $CAT(" d_", given_arg))
            }, { // ELSE:
                $CAT($UNTYPE(fn_meta_arg.type).nativeType, $CAT(" ", given_arg))
            }))
        }, $IF(isCorrectType, {

            // Check Pointer type
            $IF($EQUALS(expected_type, VALUE_REF), {
                $IF($EQUALS($UNTYPE(fn_meta_arg.type).id, $UNTYPE($UNTYPE(given_arg).type).id), {}, {
                    $RAISE($$, "Type Mismatch: Function expects " + ($UNTYPE(fn_meta_arg.type).id) + " for argument " + (fn_meta_arg.name) + ", not " + ($UNTYPE($UNTYPE(given_arg).type).id))
                })
            }, {})

            $CAT($UNTYPE(fn_meta_arg.type).nativeType, $CAT(" ", $UNTYPE(given_arg).addr))
        }, {
            $RAISE($$, "Type Mismatch: Function expects " + ($UNTYPE(fn_meta_arg.type).id) + " for argument " + (fn_meta_arg.name))
        }))
    })

    retAddr ::= $NEWADDR()
    retTypeStr ::= $UNTYPE(fn_meta.returnType).nativeType


    $WRITE("\t")
    $IF($EQUALS(retTypeStr, ""), {}, { // ELSE:
        $WRITE(retAddr)
        $WRITE(" =")
        $WRITE(retTypeStr)
        $WRITE(" ")
    })
    $WRITE("call $")
    $WRITE(fn_meta.addr)
    $WRITE("(")
    $WRITE(formatted_args join ", ")
    $WRITE(")\n")

    template ::= $QUOTE($TYPEDEF($Value(fn_meta.returnType, retAddr), X))
    $EVAL($REPLACE(template, X, $UNTYPE(fn_meta.returnType).typeName))
}